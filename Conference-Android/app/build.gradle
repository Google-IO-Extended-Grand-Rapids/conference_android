apply plugin: 'android'


// Manifest version information!
def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }

    signingConfigs {

        debug {
            storeFile file("google_io_debug.jks")
            keyAlias "google_io"
            storePassword "googleio"
            keyPassword "googleio"
        }

        release {
            storeFile file("google_io.jks")
            keyAlias "google_io"
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "API_BASE_URL", "\"http://localhost:8080\""
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "String", "API_BASE_URL", "\"http://104.236.204.59:8080\""
            signingConfig signingConfigs.release
        }
    }
}

def runTasks = gradle.startParameter.taskNames
if ('assemble' in runTasks || 'assembleRelease' in runTasks) {
    def passwd = new String(System.console().readPassword('\nKeyStore Password: '))
    android.signingConfigs.release.storePassword = passwd
    android.signingConfigs.release.keyPassword = passwd
}

dependencies {
    compile 'com.netflix.rxjava:rxjava-android:0.20.7'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.google.android.gms:play-services:4.0.30'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.jakewharton.timber:timber:2.5.1'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
